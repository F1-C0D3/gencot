#! /bin/sh

# Transfer declaration comments to single files.
# This is a filter from stdin to stdout with an additional filename argument.
# The argument file must contain output of gencot-selcomments.
# The comment files are written in the current directory.

if [ $# != 1 ]
then
  echo "Error: Single file name expected as argument."
  exit 0
fi

awk \
'BEGIN { 
   cfile = ARGV[1]; delete ARGV[1] 
   bef = 1; lnr = 0; unit = ""; ustart = 1 
   while ((getline cline < cfile) > 0) { 
     if (cline ~ /^-\}_/) { 
       if (bef) before[lnr] = unit 
       unit = substr(cline,4) 
       if (unit == "") ustart = 1 
       bef = 1 - bef 
     } 
     else { lnr++ 
       if (ustart) { unit = cline; ustart = 0 } 
       else { unit = unit "\n" cline } 
     } 
   } 
   close(cfile) 
 } 
 /^#DECL/ { 
   printf(before[$3]) > ($2 ".comment") 
 }' $1

# documentation:
#
# In the BEGIN rule we read the before comment units into the array before. The first unit is
# stored in before, then we switch whenever we find the separator -}_ at the beginning of a line.
# Whether the current unit is a before unit is specified by the boolean variable bef.
# 
# All units are collected in variable unit until the separator occurs. Before-units are stored
# in the element indexed by the current line number lnr of the separator.
#
# The line number lnr is only counted for lines without separator, since the newline immediately before
# the separator is a part of the separator and must not be counted.
#
# Variable ustart is used to avoid inserting a newline at the start of a unit. 
 
# Implementation in awk:
# - The filename argument for the comment file must be copied and removed from the argument list ARGV
#   to prevent its processing as normal input to awk. The comment file is explicitly read by getline.
# - The presence of the separator at the beginning of a line is tested by matching each line with the 
#   pattern /^-\}_/. If present it is omitted by using substr(cline,4) which starts after the 
#   separator.
# - Writing to the comment files is done with a redirected print. Since the content of before
#   already consists of lines with a newline at the end we use printf to 
#   prevent appending an additional newline at the end of the comment files.
